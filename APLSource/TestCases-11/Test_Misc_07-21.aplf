 r←Test_Misc_07(stopFlag batchFlag);rc;tmp;A;expected;stopFlag;⎕TRAP
⍝ Exercise the "RunThese" method
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←∆Failed

 A←#.APLTreeUtils

⍝ Pre-conditions
 tmp←⎕NS''
 tmp.⎕FX'r←Test_001 (x y)' '   ⍝ This and that' 'r←1'
 tmp.⎕FX'r←Test_002 (x y)' '⍝ that and this' 'r←0'
 tmp.⎕FX'r←Test_003 (x y)' '   ⍝⍝ Something else' 'r←1'
 tmp.⎕FX'r←Test_Grp1_001 (x y)' 'r←0'
 tmp.⎕FX'r←Test_Grp1_002 (x y)' 'r←1' '. ⍝ Enter →0 here!'
 tmp.⎕FX'r←Test_Grp1_003 (x y)' 'r←1' '. ⍝ Enter →0 here!'
 tmp.⎕FX'r←Test_Grp2_001 (x y)' 'r←1'
 tmp.⎕FX'r←Test_Grp2_002 (x y)' 'r←0'
 tmp.⎕FX'r←Test_Run_001 (x y)' 'r←0'
 ##.Tester.EstablishHelpersIn tmp
 tmp.Tester←##.Tester

⍝ The tests
 log←1↓tmp.RunThese 2 4
 →FailsIf~(⍴log)∊17 18
 →FailsIf 1 0 0≢∆GetTotals log

 :Trap 0
     log←1↓tmp.RunThese'Grp1'(1 3)
     →FailsIf 1
 :Else
     →PassesIf'. ⍝ Enter →0 here!'{⍺≡(-⍴⍺)↑⍵}2⊃⎕DM
 :EndTrap

 log←1↓tmp.RunThese'Grp2'
 →FailsIf~(⍴log)∊17 18 19
 →FailsIf 2 1 0≢∆GetTotals log

 log←1↓tmp.RunThese'Test_Grp2_002'
 →FailsIf~(⍴log)∊16 17 18
 →FailsIf 1 0 0≢∆GetTotals log

 log←1↓tmp.RunThese'Test_Grp'
 →PassesIf(,'0')=1⊃' 'A.Split A.dlb 10⊃log   ⍝ Because no group was identified

 log←1↓tmp.RunThese'Test_Grp*'
 →PassesIf 5 3 0≡∆GetTotals log

 log←1↓tmp.RunThese'Run'
 →FailsIf~(⍴log)∊16 17 18
 →PassesIf 1 0 0≡∆GetTotals log

 log←1↓tmp.RunThese'Test_Run'
 →FailsIf~(⍴log)∊16 17 18
 →PassesIf 1 0 0≡∆GetTotals log

 log←1↓tmp.RunThese'Test_Run*'
 →FailsIf~(⍴log)∊16 17 18
 →PassesIf 1 0 0≡∆GetTotals log

 r←∆OK
