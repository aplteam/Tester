 r←{numbers}L group;A;r2;⎕IO;⎕ML;b
⍝ Prints a list with all test cases and the first comment line to the session.
⍝ If "group" is not empty then it will print only that group (case independent).
⍝ May or may not start with "Test_"
⍝ If "numbers" is defined only those number are printed.
 ⎕IO←1 ⋄ ⎕ML←1
 :If 9=#.⎕NC'APLTreeUtils'
     A←#.APLTreeUtils
 :ElseIf 9=⎕NC'APLTreeUtils'
     A←APLTreeUtils
 :ElseIf 9=##.⎕NC'APLTreeUtils'
     A←##.APLTreeUtils
 :ElseIf 9∊⊃¨⎕RSI.⎕NC⊂'APLTreeUtils'
     A←(⊃(9=⊃¨⎕RSI.⎕NC⊂'APLTreeUtils')/⎕RSI).APLTreeUtils
 :ElseIf 9∊⊃¨⎕RSI.##.⎕NC⊂'APLTreeUtils'
     A←(⊃(9=⊃¨⎕RSI.##.⎕NC⊂'APLTreeUtils')/⎕RSI.##).APLTreeUtils
 :Else
     'Missing: APLTreeUtils'⎕SIGNAL 6
 :EndIf
 numbers←{(0<⎕NC ⍵):⍎⍵ ⋄ ⍬}'numbers'
 r2←↓'Test_'{⍵⌿⍨((⍴⍺)↑[2]⍵)∧.=⍺}'T'⎕NL 3
 :If ~0∊⍴group
     group←A.Lowercase'test_'{((⍺≢(⍴⍺)↑⍵)/⍺),⍵}group
     r2←(({⎕ML←1 ⋄ ↑⍵}A.Lowercase(⍴group)↑¨r2)∧.=group)⌿r2
 :EndIf
 :If 0∊⍴r2
     r←0 1⍴''
 :Else
     :If ~0∊⍴numbers
         r2←(({⍎⍵↑⍨-(-1)+'_'⍳⍨⌽⍵}¨r2)∊numbers)⌿r2
     :EndIf
     r2←r2,[1.5]{{⍵↓⍨+/∧\' '=⍵}{⍵↓⍨⍵⍳'⍝'}{⎕ML←3 ⋄ ∊⍵}1↑1↓⎕NR ⍵}¨r2
     r2←r2[⍋↑A.Lowercase r2[;1];]
     r←⎕FMT r2
     :If ⎕PW<2⊃⍴r
         b←⎕PW<⊃∘⍴¨A.dtb↓r
         (b⌿r)←↑(2⊃⍴r)↑¨((⎕PW-3)↑¨↓b⌿r),¨⊂'...'
         r←⎕PW↑[2]r
     :EndIf
 :EndIf
